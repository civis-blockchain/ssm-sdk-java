allprojects {
    apply plugin: 'idea'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    group = 'org.civis-blockchain.ssm'
    version = version

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenLocal()
        mavenCentral()
    }

    ext {
        fabric_version = '1.4.0'
    }

    ext {
        guava_version = '27.0.1-jre'
        slf4j_version = '1.7.26'
        assertj_version = '3.12.0'
        junit_version = '5.4.0'
        jackson_version = '2.9.8'
    }

    dependencies {
        implementation "com.google.guava:guava:$guava_version"
        implementation "com.fasterxml.jackson.core:jackson-databind:$jackson_version"

        implementation "org.slf4j:slf4j-api:$slf4j_version"

        testImplementation "org.assertj:assertj-core:$assertj_version"
        testImplementation "org.junit.jupiter:junit-jupiter:$junit_version"
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allJava
        archiveClassifier = 'sources'
    }

    afterEvaluate { Project project ->
        publishing {
            publications {
                mavenJava(MavenPublication) {
                    artifactId = project.artifactId
                    from components.java
                    artifact sourcesJar
                    versionMapping {
                        usage('java-api') {
                            fromResolutionOf('runtimeClasspath')
                        }
                        usage('java-runtime') {
                            fromResolutionResult()
                        }
                    }
                    pom {
                        name = 'Ssm SDK'
                        description = 'A SDK to interact with signing state machines chaincode developped for Hyperleger Fabric.  '
                        url = 'https://github.com/civis-blockchain/blockchain-ssm'
                        licenses {
                            license {
                                name = 'The Apache License, Version 2.0'
                                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            }
                        }
                        developers {
                            developer {
                                id = 'adrienp'
                                name = 'Adrien Poupard'
                            }
                        }
                        scm {
                            connection = 'scm:git:git://https://github.com/civis-blockchain/blockchain-ssm.git'
                            developerConnection = 'scm:git:https://github.com/civis-blockchain/blockchain-ssm.git'
                            url = 'https://github.com/civis-blockchain/blockchain-ssm'
                        }
                    }
                }
            }
            repositories {
                maven {
                    // change URLs to point to your repos, e.g. http://my.org/repo
                    def releasesRepoUrl = "$mavenRepo/repositories/releases"
                    def snapshotsRepoUrl = "$mavenRepo/repositories/snapshots"
                    url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                    credentials {
                        username sonatypeUsername
                        password sonatypePassword
                    }
                }
            }
        }
        signing {
            sign publishing.publications.mavenJava
        }

    }
}
